!! <<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<< !!
!! Copyright (C) 2021 Oleg Butakov
!!
!! Permission is hereby granted, free of charge, to any person
!! obtaining a copy of this software and associated documentation
!! files (the "Software"), to deal in the Software without
!! restriction, including without limitation the rights  to use,
!! copy, modify, merge, publish, distribute, sublicense, and/or
!! sell copies of the Software, and to permit persons to whom the 
!! Software is furnished to do so, subject to the following
!! conditions:
!!
!! The above copyright notice and this permission notice shall be
!! included in all copies or substantial portions of the Software.
!!
!! THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND,
!! EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES
!! OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND
!! NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT
!! HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY,
!! WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING
!! FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR
!! OTHER DEALINGS IN THE SOFTWARE.
!! >>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>> !!
module StormRuler_Solvers_LSQR

#$use 'StormRuler_Params.fi'

use StormRuler_Parameters, only: dp

use StormRuler_Mesh, only: tMesh
use StormRuler_Array, only: tArray, AllocArray

use StormRuler_BLAS, only: Norm_2, Fill, Set, Scale, Add, Sub
use StormRuler_BLAS, only: tMatVecFunc

use StormRuler_ConvParams, only: tConvParams
use StormRuler_Preconditioner, only: tPreconditioner

!! <<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<< !!
!! >>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>> !!

implicit none

interface Solve_LSQR
  module procedure Solve_LSQR
  module procedure Solve_SymmLSQR
end interface Solve_LSQR

interface Solve_LSMR
  module procedure Solve_LSMR
  module procedure Solve_SymmLSMR
end interface Solve_LSMR

!! <<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<< !!
!! >>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>> !!

contains

!! -=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=- !! 
!! Solve a right preconditioned linear least squares problem:
!! â€–ğ“[ğ“Ÿ]ğ’š - ğ’ƒâ€–â‚‚ â†’ ğ˜®ğ˜ªğ˜¯, ğ’™ = [ğ“Ÿ]ğ’š, using the LSQR method.
!!
!! LSQR is algebraically equivalent to applying CG
!! to the normal equations: (ğ“[ğ“Ÿ])*ğ“[ğ“Ÿ]ğ’š = (ğ“[ğ“Ÿ])*ğ’ƒ, ğ’™ = [ğ“Ÿ]ğ’š,
!! (or, equivalently, [ğ“Ÿ*]ğ“*ğ“[ğ“Ÿ]ğ’š = [ğ“Ÿ*]ğ“*ğ’ƒ, ğ’™ = [ğ“Ÿ]ğ’š),
!! but but has better numerical properties.
!!
!! The residual norm â€–ğ“[ğ“Ÿ]ğ’š - ğ’ƒâ€–â‚‚ decreases monotonically, 
!! while the normal equation'sArr residual norm â€–(ğ“[ğ“Ÿ])*(ğ“[ğ“Ÿ]ğ’š - ğ’ƒ)â€– 
!! is not guaranteed to decrease.
!!
!! References:
!! [1] Paige, C. and M. Saunders. 
!!     â€œLSQR: An Algorithm for Sparse Linear Equations and 
!!     Sparse Least Squares.â€ ACM Trans. Math. Softw. 8 (1982): 43-71.
!! [2] Karimi, S., D. K. Salkuyeh and F. Toutounian. 
!!     â€œA preconditioner for the LSQR algorithm.â€ 
!!     Journal of applied mathematics & informatics 26 (2008): 213-222.
!! -=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=- !! 
subroutine Solve_LSQR(mesh, xArr, bArr, MatVec, &
    & ConjMatVec, params, pre, conjPre)
  class(tMesh), intent(in) :: mesh
  class(tArray), intent(in) :: bArr
  class(tArray), intent(inout) :: xArr
  class(tConvParams), intent(inout) :: params
  class(tPreconditioner), intent(inout), optional :: pre, conjPre
  procedure(tMatVecFunc) :: MatVec, ConjMatVec
  
  real(dp) :: alpha, beta, rho, rhoBar, theta, phi, phiBar, phiTilde, cs, sn
  type(tArray) :: sArr, tArr, rArr, uArr, vArr, wArr, zArr
  
  call AllocArray(tArr, rArr, uArr, vArr, wArr, zArr, mold=xArr)
  if (present(pre)) then
    call AllocArray(sArr, mold=xArr)
    call pre%Init(mesh, MatVec)
    call conjPre%Init(mesh, MatVec)
  end if

  ! ----------------------
  ! Utilize the initial guess.
  ! Consider the decomposition:
  ! ğ’™ = ğ’™â‚€ + ğ’›. (*)
  ! Substituting (*) into the equation, we get:
  ! ğ“[ğ“Ÿ]ğ’š = ğ’“, where: ğ’› = [ğ“Ÿ]ğ’š, ğ’“ = ğ’ƒ - ğ“ğ’™â‚€.
  ! The last equations can be solved with ğ’šâ‚€ = {0}áµ€.
  ! ----------------------

  ! ----------------------
  ! Initialize:
  ! ğ’“ â† ğ“ğ’™,
  ! ğ’“ â† ğ’ƒ - ğ’“,
  ! ğ›½ â† â€–ğ’“â€–, ğ’– â† ğ’“/ğ›½,
  ! ğ—¶ğ—³ ğ“Ÿ â‰  ğ—»ğ—¼ğ—»ğ—²: 
  !   ğ’” â† ğ“*ğ’–, ğ’• â† ğ“Ÿ*ğ’”, 
  ! ğ—²ğ—¹ğ˜€ğ—²: ğ’• â† ğ“*ğ’–, ğ—²ğ—»ğ—± ğ—¶ğ—³
  ! ğ›¼ â† â€–ğ’•â€–, ğ’— â† ğ’•/ğ›¼.
  ! ----------------------
  call MatVec(mesh, rArr, xArr)
  call Sub(mesh, rArr, bArr, rArr)
  beta = Norm_2(mesh, rArr); call Scale(mesh, uArr, rArr, 1.0_dp/beta)
  if (present(pre)) then
    call ConjMatVec(mesh, sArr, uArr)
    call conjPre%Apply(mesh, tArr, sArr, ConjMatVec)
  else
    call ConjMatVec(mesh, tArr, uArr)
  end if
  alpha = Norm_2(mesh, tArr); call Scale(mesh, vArr, tArr, 1.0_dp/alpha)

  ! ----------------------
  ! ğœ‘Ì… â† ğ›½, ğœŒÌ… â† ğ›¼.
  ! ğ’› â† {0}áµ€,
  ! ğ’˜ â† ğ’—,
  ! ----------------------
  phiBar = beta; rhoBar = alpha
  call Fill(mesh, zArr, 0.0_dp)
  call Set(mesh, wArr, vArr)

  ! ----------------------
  ! ğœ‘Ìƒ â† ğœ‘Ì…,
  ! Check convergence for ğœ‘Ìƒ.
  ! ----------------------
  phiTilde = phiBar
  if (params%Check(phiTilde)) return
  
  do
    ! ----------------------
    ! Continue the bidiagonalization:
    ! ğ—¶ğ—³ ğ“Ÿ â‰  ğ—»ğ—¼ğ—»ğ—²: 
    !   ğ’” â† ğ“Ÿğ’—, ğ’• â† ğ“ğ’”,
    ! ğ—²ğ—¹ğ˜€ğ—²: ğ’• â† ğ“ğ’—, ğ—²ğ—»ğ—± ğ—¶ğ—³
    ! ğ’• â† ğ’• - ğ›¼ğ’–,
    ! ğ›½ â† â€–ğ’•â€–, ğ’– â† ğ’•/ğ›½,
    ! ğ—¶ğ—³ ğ“Ÿ â‰  ğ—»ğ—¼ğ—»ğ—²:
    !   ğ’” â† ğ“*ğ’–, ğ’• â† ğ“Ÿ*ğ’”, 
    ! ğ—²ğ—¹ğ˜€ğ—²: ğ’• â† ğ“*ğ’–, ğ—²ğ—»ğ—± ğ—¶ğ—³
    ! ğ’• â† ğ’• - ğ›½ğ’—,
    ! ğ›¼ â† â€–ğ’•â€–, ğ’— â† ğ’•/ğ›¼.
    ! ----------------------
    if (present(pre)) then
      call pre%Apply(mesh, sArr, vArr, MatVec)
      call MatVec(mesh, tArr, sArr)
    else
      call MatVec(mesh, tArr, vArr)
    end if
    call Sub(mesh, tArr, tArr, uArr, alpha)
    beta = Norm_2(mesh, tArr); call Scale(mesh, uArr, tArr, 1.0_dp/beta)
    if (present(pre)) then
      call ConjMatVec(mesh, sArr, uArr)
      call conjPre%Apply(mesh, tArr, sArr, ConjMatVec)
    else
      call ConjMatVec(mesh, tArr, uArr)
    end if
    call Sub(mesh, tArr, tArr, vArr, beta)
    alpha = Norm_2(mesh, tArr); call Scale(mesh, vArr, tArr, 1.0_dp/alpha)
    
    ! ----------------------
    ! Construct and apply rotation:
    ! ğœŒ â† (ğœŒÌ…Â² + ğ›½Â²)Â¹áŸÂ²,
    ! ğ‘ğ‘  â† ğœŒÌ…/ğœŒ, ğ‘ ğ‘› â† ğ›½/ğœŒ,
    ! ğœƒ â† ğ‘ ğ‘›â‹…ğ›¼, ğœŒÌ… â† -ğ‘ğ‘ â‹…ğ›¼,
    ! ğœ‘ â† ğ‘ğ‘ â‹…ğœ‘, ğœ‘Ì… â† ğ‘ ğ‘›â‹…ğœ‘Ì….
    ! ----------------------
    rho = hypot(rhoBar, beta)
    cs = rhoBar/rho; sn = beta/rho
    theta = sn*alpha; rhoBar = -cs*alpha
    phi = cs*phiBar; phiBar = sn*phiBar

    ! ----------------------
    ! Update ğ’›-solution:
    ! ğ’› â† ğ’› + (ğœ‘/ğœŒ)ğ’˜,
    ! ğ’˜ â† ğ’— - (ğœƒ/ğœŒ)ğ’˜.
    ! Check convergence for ğœ‘Ì… and ğœ‘Ì…/ğœ‘Ìƒ.
    ! ( ğœ‘Ì… and ğœ‘Ìƒ implicitly contain residual norms;
    !   ğœ‘Ì…|ğœŒÌ…| implicitly contain (ğ“[ğ“Ÿ])*-residual norms, â€–(ğ“[ğ“Ÿ])*ğ’“â€–. )
    ! ----------------------
    call Add(mesh, zArr, zArr, wArr, phi/rho)
    call Sub(mesh, wArr, vArr, wArr, theta/rho)
    if (params%Check(phiBar, phiBar/phiTilde)) exit
  end do

  ! ----------------------
  ! Compute ğ’™-solution:
  ! ğ—¶ğ—³ ğ“Ÿ â‰  ğ—»ğ—¼ğ—»ğ—²:
  !   ğ’• â† ğ“Ÿğ’›, ğ’™ â† ğ’™ + ğ’•.
  ! ğ—²ğ—¹ğ˜€ğ—²: ğ’™ â† ğ’™ + ğ’›. ğ—²ğ—»ğ—± ğ—¶ğ—³
  ! ----------------------
  if (present(pre)) then
    call pre%Apply(mesh, tArr, zArr, MatVec)
    call Add(mesh, xArr, xArr, tArr)
  else
    call Add(mesh, xArr, xArr, zArr)
  end if

end subroutine Solve_LSQR

!! -=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=- !! 
!! Solve a right preconditioned linear self-adjoint: least squares
!! problem: â€–ğ“[ğ“Ÿ]ğ’š - ğ’ƒâ€–â‚‚ â†’ ğ˜®ğ˜ªğ˜¯, ğ’™ = [ğ“Ÿ]ğ’š, using the LSQR method.
!!
!! LSQR is not recommended in the self-adjoint case,
!! please consider MINRES instead.
!! -=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=- !! 
subroutine Solve_SymmLSQR(mesh, xArr, bArr, MatVec, params, pre)
  class(tMesh), intent(in) :: mesh
  class(tArray), intent(in) :: bArr
  class(tArray), intent(inout) :: xArr
  class(tConvParams), intent(inout) :: params
  class(tPreconditioner), intent(inout), optional :: pre
  procedure(tMatVecFunc) :: MatVec

  if (present(pre)) then
    call Solve_LSQR(mesh, xArr, bArr, MatVec, MatVec, params, pre, pre)
  else
    call Solve_LSQR(mesh, xArr, bArr, MatVec, MatVec, params)
  end if
  
end subroutine Solve_SymmLSQR

!! -=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=- !! 
!! Solve a right preconditioned linear least squares problem:
!! â€–ğ“[ğ“Ÿ]ğ’š - ğ’ƒâ€–â‚‚ â†’ ğ˜®ğ˜ªğ˜¯, ğ’™ = [ğ“Ÿ]ğ’š, using the LSMR method.
!!
!! LSMR is algebraically equivalent to applying MINRES 
!! to the normal equations: (ğ“[ğ“Ÿ])*ğ“[ğ“Ÿ]ğ’š = (ğ“[ğ“Ÿ])*ğ’ƒ, ğ’™ = [ğ“Ÿ]ğ’š, 
!! (or, equivalently, [ğ“Ÿ*]ğ“*ğ“[ğ“Ÿ]ğ’š = [ğ“Ÿ*]ğ“*ğ’ƒ, ğ’™ = [ğ“Ÿ]ğ’š),
!! but but has better numerical properties.
!! 
!! The normal equation'sArr residual norm â€–(ğ“[ğ“Ÿ])*(ğ“[ğ“Ÿ]ğ’š - ğ’ƒ)â€– 
!! decreases monotonically, while the residual norm â€–ğ“[ğ“Ÿ]ğ’š - ğ’ƒâ€–â‚‚   
!! is not guaranteed to decrease (but decreases on practice).
!!
!! References:
!! [1] Fong, D. C. and M. Saunders. 
!!     â€œLSMR: An Iterative Algorithm for Sparse Least-Squares Problems.â€ 
!!     SIAM J. Sci. Comput. 33 (2011): 2950-2971.
!! [2] Karimi, S., D. K. Salkuyeh and F. Toutounian. 
!!     â€œA preconditioner for the LSQR algorithm.â€ 
!!     Journal of applied mathematics & informatics 26 (2008): 213-222.
!! -=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=- !! 
subroutine Solve_LSMR(mesh, xArr, bArr, MatVec, &
    & ConjMatVec, params, pre, conjPre)
  class(tMesh), intent(in) :: mesh
  class(tArray), intent(in) :: bArr
  class(tArray), intent(inout) :: xArr
  class(tConvParams), intent(inout) :: params
  class(tPreconditioner), intent(inout), optional :: pre, conjPre
  procedure(tMatVecFunc) :: MatVec, ConjMatVec

  real(dp) :: alpha, alphaBar, beta, rho, rhoBar, cs, sn, &
    & theta, thetaBar, psi, psiBar, psiTilde, zeta, csBar, snBar
  type(tArray) :: rArr, sArr, tArr, wArr, h, uArr, vArr, zArr
  
  call AllocArray(tArr, rArr, uArr, vArr, wArr, h, zArr, mold=xArr)
  if (present(pre)) then
    call AllocArray(sArr, mold=xArr)
    call pre%Init(mesh, MatVec)
    call conjPre%Init(mesh, MatVec)
  end if

  ! ----------------------
  ! Utilize the initial guess.
  ! Consider the decomposition:
  ! ğ’™ = ğ’™â‚€ + ğ’›. (*)
  ! Substituting (*) into the equation, we get:
  ! ğ“[ğ“Ÿ]ğ’š = ğ’“, where: ğ’› = [ğ“Ÿ]ğ’š, ğ’“ = ğ’ƒ - ğ“ğ’™â‚€.
  ! The last equations can be solved with ğ’šâ‚€ = {0}áµ€.
  ! ----------------------

  ! ----------------------
  ! Initialize:
  ! ğ’“ â† ğ“ğ’™,
  ! ğ’“ â† ğ’ƒ - ğ’“,
  ! ğ›½ â† â€–ğ’“â€–, ğ’– â† ğ’“/ğ›½,
  ! ğ—¶ğ—³ ğ“Ÿ â‰  ğ—»ğ—¼ğ—»ğ—²: 
  !   ğ’” â† ğ“*ğ’–, ğ’• â† ğ“Ÿ*ğ’”, 
  ! ğ—²ğ—¹ğ˜€ğ—²: ğ’• â† ğ“*ğ’–, ğ—²ğ—»ğ—± ğ—¶ğ—³
  ! ğ›¼ â† â€–ğ’•â€–, ğ’— â† ğ’•/ğ›¼.
  ! ----------------------
  call MatVec(mesh, rArr, xArr)
  call Sub(mesh, rArr, bArr, rArr)
  beta = Norm_2(mesh, rArr); call Scale(mesh, uArr, rArr, 1.0_dp/beta)
  if (present(pre)) then
    call ConjMatVec(mesh, sArr, uArr)
    call conjPre%Apply(mesh, tArr, sArr, ConjMatVec)
  else
    call ConjMatVec(mesh, tArr, uArr)
  end if
  alpha = Norm_2(mesh, tArr); call Scale(mesh, vArr, tArr, 1.0_dp/alpha)

  ! ----------------------
  ! ğ›¼Ì… â† ğ›¼, ğœ“Ì… â† ğ›¼ğ›½,
  ! ğœ â† 1, ğ‘Ì…ğ‘ Ì… â† 1, ğ‘ Ì…ğ‘›Ì… â† 0,
  ! ğ’› â† {0}áµ€,
  ! ğ’˜ â† ğ’—, ğ’‰ â† {0}áµ€.
  ! ----------------------
  alphaBar = alpha; psiBar = alpha*beta
  zeta = 1.0_dp; csBar = 1.0_dp; snBar = 0.0_dp
  call Fill(mesh, zArr, 0.0_dp)
  call Set(mesh, wArr, vArr); call Fill(mesh, h, 0.0_dp)

  ! ----------------------
  ! ğœ“Ìƒ â† ğœ“Ì…,
  ! Check convergence for ğœ“Ìƒ.
  ! ----------------------
  psiTilde = psiBar
  if (params%Check(psiTilde)) return
  
  do
    ! ----------------------
    ! Continue the bidiagonalization:
    ! ğ—¶ğ—³ ğ“Ÿ â‰  ğ—»ğ—¼ğ—»ğ—²: 
    !   ğ’” â† ğ“Ÿğ’—, ğ’• â† ğ“ğ’”,
    ! ğ—²ğ—¹ğ˜€ğ—²: ğ’• â† ğ“ğ’—, ğ—²ğ—»ğ—± ğ—¶ğ—³
    ! ğ’• â† ğ’• - ğ›¼ğ’–,
    ! ğ›½ â† â€–ğ’•â€–, ğ’– â† ğ’•/ğ›½,
    ! ğ—¶ğ—³ ğ“Ÿ â‰  ğ—»ğ—¼ğ—»ğ—²:
    !   ğ’” â† ğ“*ğ’–, ğ’• â† ğ“Ÿ*ğ’”, 
    ! ğ—²ğ—¹ğ˜€ğ—²: ğ’• â† ğ“*ğ’–, ğ—²ğ—»ğ—± ğ—¶ğ—³
    ! ğ’• â† ğ’• - ğ›½ğ’—,
    ! ğ›¼ â† â€–ğ’•â€–, ğ’— â† ğ’•/ğ›¼.
    ! ----------------------
    if (present(pre)) then
      call pre%Apply(mesh, sArr, vArr, MatVec)
      call MatVec(mesh, tArr, sArr)
    else
      call MatVec(mesh, tArr, vArr)
    end if
    call Sub(mesh, tArr, tArr, uArr, alpha)
    beta = Norm_2(mesh, tArr); call Scale(mesh, uArr, tArr, 1.0_dp/beta)
    if (present(pre)) then
      call ConjMatVec(mesh, sArr, uArr)
      call conjPre%Apply(mesh, tArr, sArr, ConjMatVec)
    else
      call ConjMatVec(mesh, tArr, uArr)
    end if
    call Sub(mesh, tArr, tArr, vArr, beta)
    alpha = Norm_2(mesh, tArr); call Scale(mesh, vArr, tArr, 1.0_dp/alpha)
    
    ! ----------------------
    ! Construct and apply rotations:
    ! ğœŒ â† (ğ›¼Ì…Â² + ğ›½Â²)Â¹áŸÂ²,
    ! ğ‘ğ‘  â† ğ›¼Ì…/ğœŒ, ğ‘ ğ‘› â† ğ›½/ğœŒ,
    ! ğœƒ â† ğ‘ ğ‘›â‹…ğ›¼, ğ›¼Ì… â† ğ‘ğ‘ â‹…ğ›¼,
    ! ğœƒÌ… â† ğ‘ Ì…ğ‘›Ì…â‹…ğœŒ, ğœŒÌ… â† [(ğ‘Ì…ğ‘ Ì…â‹…ğœŒ)Â² + ğœƒÂ²]Â¹áŸÂ²,
    ! ğ‘Ì…ğ‘ Ì… â† ğ‘Ì…ğ‘ Ì…â‹…ğœŒ/ğœŒÌ…, ğ‘ Ì…ğ‘›Ì… â† ğœƒ/ğœŒÌ…,
    ! ğœ“ â† ğ‘Ì…ğ‘ Ì…â‹…ğœ“Ì…, ğœ“Ì… â† -ğ‘ Ì…ğ‘›Ì…â‹…ğœ“Ì….
    ! ----------------------
    rho = hypot(alphaBar, beta)
    cs = alphaBar/rho; sn = beta/rho
    theta = sn*alpha; alphaBar = cs*alpha
    thetaBar = snBar*rho; rhoBar = hypot(csBar*rho, theta)
    csBar = csBar*rho/rhoBar; snBar = theta/rhoBar
    psi = csBar*psiBar; psiBar = -snBar*psiBar

    ! ----------------------
    ! Update ğ’›-solution:
    ! ğ’‰ â† ğ’˜ - (ğœƒğœŒ/ğœ)ğ’‰, ğœ â† ğœŒğœŒÌ…,
    ! ğ’› â† ğ’› + (ğœ“/ğœ)ğ’‰,
    ! ğ’˜ â† ğ’— - (ğœƒ/ğœŒ)ğ’˜.
    ! Check convergence for |ğœ“Ì…| and |ğœ“Ì…/ğœ“Ìƒ|.
    ! ( |ğœ“Ì…| and |ğœ“Ìƒ| implicitly contain (ğ“[ğ“Ÿ])*-residual norms, â€–(ğ“[ğ“Ÿ])*ğ’“â€–. )
    ! ----------------------
    call Sub(mesh, h, wArr, h, thetaBar*rho/zeta); zeta = rho*rhoBar
    call Add(mesh, zArr, zArr, h, psi/zeta)
    call Sub(mesh, wArr, vArr, wArr, theta/rho)
    if (params%Check(abs(psiBar), abs(psiBar/psiTilde))) exit
  end do

  ! ----------------------
  ! Compute ğ’™-solution:
  ! ğ—¶ğ—³ ğ“Ÿ â‰  ğ—»ğ—¼ğ—»ğ—²:
  !   ğ’• â† ğ“Ÿğ’›, ğ’™ â† ğ’™ + ğ’•.
  ! ğ—²ğ—¹ğ˜€ğ—²: ğ’™ â† ğ’™ + ğ’›. ğ—²ğ—»ğ—± ğ—¶ğ—³
  ! ----------------------
  if (present(pre)) then
    call pre%Apply(mesh, tArr, zArr, MatVec)
    call Add(mesh, xArr, xArr, tArr)
  else
    call Add(mesh, xArr, xArr, zArr)
  end if

end subroutine Solve_LSMR

!! -=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=- !! 
!! Solve a right preconditioned linear self-adjoint: least squares
!! problem: â€–ğ“[ğ“Ÿ]ğ’š - ğ’ƒâ€–â‚‚ â†’ ğ˜®ğ˜ªğ˜¯, ğ’™ = [ğ“Ÿ]ğ’š, using the LSMR method.
!!
!! Using LSMR is not recommended in the self-adjoint case,
!! please consider MINRES instead.
!! -=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=- !! 
subroutine Solve_SymmLSMR(mesh, xArr, bArr, MatVec, params, pre)
  class(tMesh), intent(in) :: mesh
  class(tArray), intent(in) :: bArr
  class(tArray), intent(inout) :: xArr
  class(tConvParams), intent(inout) :: params
  class(tPreconditioner), intent(inout), optional :: pre
  procedure(tMatVecFunc) :: MatVec

  if (present(pre)) then
    call Solve_LSMR(mesh, xArr, bArr, MatVec, MatVec, params, pre, pre)
  else
    call Solve_LSMR(mesh, xArr, bArr, MatVec, MatVec, params)
  end if
  
end subroutine Solve_SymmLSMR

end module StormRuler_Solvers_LSQR
