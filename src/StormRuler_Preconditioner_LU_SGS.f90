!! <<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<< !!
!! Copyright (C) 2021 Oleg Butakov
!!
!! Permission is hereby granted, free of charge, to any person
!! obtaining a copy of this software and associated documentation
!! files (the "Software"), to deal in the Software without
!! restriction, including without limitation the rights  to use,
!! copy, modify, merge, publish, distribute, sublicense, and/or
!! sell copies of the Software, and to permit persons to whom the
!! Software is furnished to do so, subject to the following
!! conditions:
!!
!! The above copyright notice and this permission notice shall be
!! included in all copies or substantial portions of the Software.
!!
!! THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND,
!! EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES
!! OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND
!! NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT
!! HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY,
!! WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING
!! FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR
!! OTHER DEALINGS IN THE SOFTWARE.
!! >>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>> !!
module StormRuler_Preconditioner_LU_SGS

use StormRuler_Consts, only: ip, dp
use StormRuler_Parameters, only: gMaxIterLU_SGS

use StormRuler_Helpers, only: ErrorStop

use StormRuler_Mesh, only: tMesh
use StormRuler_Array, only: tArray, AllocArray, FreeArray

use StormRuler_BLAS, only: tMatVecFunc, Set, Add

use StormRuler_Matrix, only: tMatrix, tParallelTriangularContext, &
  & PartialMatrixVector, SolveDiag, &
  & InitParallelTriangularContext, SolveTriangular
use StormRuler_Preconditioner, only: tMatrixBasedPreconditioner

!! <<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<< !!
!! >>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>> !!

implicit none

!! -=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=- !!
!! Block LU Symmetric Gauss-Seidel (LU-SGS) preconditioner.
!!
!! Consider the decomposition: ğ“ = ğ“› + ğ““ + ğ“¤, where ğ““ is the 
!! (block-)diagonal of ğ“, ğ“› and ğ“¤ are lower and upper strict
!! (block-)triangular parts of ğ“. Then the equation ğ“ğ’š = ğ’ƒ can 
!! be factored as:
!! 
!! (ğ“› + ğ““)ğ““â»Â¹(ğ““ + ğ“¤)ğ’š = ğ’ƒ + ğ“›ğ““â»Â¹ğ“¤ğ’š.
!! 
!! The classical LU-SGS iterations for solving the equation ğ“ğ’š = ğ’ƒ
!! are based on following algorithm:
!! 
!! (ğ“› + ğ““)ğ’• = ğ’ƒ + ğ“›ğ““â»Â¹ğ“¤ğ’š,
!! (ğ““ + ğ“¤)ğ’šÌ‚ = ğ““ğ’•,
!! 
!! where ğ’š and ğ’šÌ‚ are the current and updated solution vectors.
!! 
!! In preconditioning, computation of the vector ğ’š = ğ“Ÿğ’™ â‰ˆ ğ“â»Â¹ğ’™ 
!! can be organized with a few LU-SGS iterations with ğ’ƒ = ğ’™ 
!! with zero initial guess. The number of inner iterations is 
!! controlled by the parameter `MaxIterLU_SGS`. In most practical 
!! cases, a single LU-SGS iteration is enough for the considarable 
!! preconditioning. More than 5 LU-SGS iterations is not recommended 
!! due to the numerical instability issues.
!! 
!! Like the other triangular matrix-based preconditioners, LU-SGS 
!! may suffer from poor parallel scaling.
!! 
!! References:
!! [1] ???
!! -=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=- !!
type, extends(tMatrixBasedPreconditioner) :: tPreconditioner_LU_SGS
  type(tMatrix), pointer, private :: Mat => null()
  type(tParallelTriangularContext), private :: LowerCtx, UpperCtx

contains
  procedure, non_overridable :: SetMatrix => SetPreconditionerMatrix_LU_SGS
  procedure, non_overridable :: Init => InitPreconditioner_LU_SGS
  procedure, non_overridable :: Apply => ApplyPreconditioner_LU_SGS

end type tPreconditioner_LU_SGS

!! <<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<< !!
!! >>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>> !!

contains

!! -=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=- !!
!! Set the LU-SGS preconditioner matrix.
!! -=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=- !!
subroutine SetPreconditionerMatrix_LU_SGS(pre, mat)
  class(tPreconditioner_LU_SGS), intent(inout) :: pre
  class(tMatrix), intent(inout), target :: mat

  pre%Mat => mat

end subroutine SetPreconditionerMatrix_LU_SGS

!! -=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=- !!
!! Initialize the LU-SGS preconditioner: ğ“Ÿ â† ğ˜ªğ˜¯ğ˜ªğ˜µ(ğ“).
!! -=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=- !!
subroutine InitPreconditioner_LU_SGS(pre, mesh, MatVec)
  class(tPreconditioner_LU_SGS), intent(inout) :: pre
  class(tMesh), intent(in), target :: mesh
  procedure(tMatVecFunc) :: MatVec

  if (.not.associated(pre%Mat)) then
    call ErrorStop('Matrix for the LU-SGS preconditioner is not set.')
  end if

  if (.not.allocated(pre%LowerCtx%LevelAddrs)) &
    & call InitParallelTriangularContext(mesh, 'L', pre%mat, pre%LowerCtx)
  if (.not.allocated(pre%UpperCtx%LevelAddrs)) &
    & call InitParallelTriangularContext(mesh, 'U', pre%mat, pre%UpperCtx)

end subroutine InitPreconditioner_LU_SGS

!! -=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=- !!
!! Apply the LU-SGS preconditioner: ğ’š â† ğ“Ÿ(ğ“)ğ’™.
!! -=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=- !!
subroutine ApplyPreconditioner_LU_SGS(pre, mesh, yArr, xArr, MatVec)
  class(tPreconditioner_LU_SGS), intent(inout) :: pre
  class(tMesh), intent(in), target :: mesh
  class(tArray), intent(inout), target :: xArr, yArr
  procedure(tMatVecFunc) :: MatVec

  integer(ip) :: k
  type(tArray) :: tArr

  if (.not.associated(pre%Mat)) then
    call ErrorStop('Matrix for the LU-SGS preconditioner is not set.')
  end if

  call AllocArray(tArr, mold=xArr)

  ! ----------------------
  ! First LU-SGS iteration:
  ! ğ’š â† (ğ“› + ğ““)â»Â¹ğ’™,
  ! ğ’• â† ğ““ğ’š,
  ! ğ’š â† (ğ““ + ğ“¤)â»Â¹ğ’•.
  ! ----------------------
  call SolveTriangular(mesh, 'L', pre%Mat, pre%LowerCtx, yArr, xArr)
  call PartialMatrixVector(mesh, 'D', pre%Mat, tArr, yArr)
  call SolveTriangular(mesh, 'U', pre%Mat, pre%UpperCtx, yArr, tArr)

  do k = 2, gMaxIterLU_SGS
    ! ----------------------
    ! Full LU-SGS iterations:
    ! ğ’• â† ğ“¤ğ’š, 
    ! ğ’š â† ğ““â»Â¹ğ’•, 
    ! ğ’• â† ğ“›ğ’š, 
    ! ğ’• â† ğ’• + ğ’™,
    ! ----------------------
    call PartialMatrixVector(mesh, 'U', pre%Mat, tArr, yArr)
    call SolveDiag(mesh, pre%Mat, yArr, tArr)
    call PartialMatrixVector(mesh, 'L', pre%Mat, tArr, yArr)
    call Add(mesh, tArr, tArr, xArr)

    ! ----------------------
    ! ğ’š â† (ğ“› + ğ““)â»Â¹ğ’•,
    ! ğ’• â† ğ““ğ’š,
    ! ğ’š â† (ğ““ + ğ“¤)â»Â¹ğ’•.
    ! ----------------------
    call SolveTriangular(mesh, 'L', pre%Mat, pre%LowerCtx, yArr, tArr)
    call PartialMatrixVector(mesh, 'D', pre%Mat, tArr, yArr)
    call SolveTriangular(mesh, 'U', pre%Mat, pre%UpperCtx, yArr, tArr)
  end do

end subroutine ApplyPreconditioner_LU_SGS

end module StormRuler_Preconditioner_LU_SGS
