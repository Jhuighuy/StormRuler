!! <<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<< !!
!! Copyright (C) 2021 Oleg Butakov
!!
!! Permission is hereby granted, free of charge, to any person
!! obtaining a copy of this software and associated documentation
!! files (the "Software"), to deal in the Software without
!! restriction, including without limitation the rights  to use,
!! copy, modify, merge, publish, distribute, sublicense, and/or
!! sell copies of the Software, and to permit persons to whom the 
!! Software is furnished to do so, subject to the following
!! conditions:
!!
!! The above copyright notice and this permission notice shall be
!! included in all copies or substantial portions of the Software.
!!
!! THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND,
!! EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES
!! OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND
!! NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT
!! HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY,
!! WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING
!! FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR
!! OTHER DEALINGS IN THE SOFTWARE.
!! >>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>> !!
module StormRuler_Solvers_LSQR

#$use 'StormRuler_Params.fi'

use StormRuler_Parameters, only: dp
use StormRuler_Mesh, only: tMesh
use StormRuler_BLAS, only: Norm_2, Fill, Set, Scale, Add, Sub
#$for T, _ in SCALAR_TYPES
use StormRuler_BLAS, only: tMatVecFunc$T
use StormRuler_Solvers_Precond, only: tPrecondFunc$T
#$end for
use StormRuler_ConvParams, only: tConvParams

!! <<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<< !!
!! >>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>> !!

implicit none

interface Solve_LSQR
  module procedure Solve_LSQR
  module procedure Solve_SymmLSQR
end interface Solve_LSQR

interface Solve_LSMR
  module procedure Solve_LSMR
  module procedure Solve_SymmLSMR
end interface Solve_LSMR

!! <<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<< !!
!! >>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>> !!

contains

!! -=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=- !! 
!! Solve a right preconditioned linear least squares problem:
!! â€–ğ“[ğ“Ÿ]ğ’š - ğ’ƒâ€–â‚‚ â†’ ğ˜®ğ˜ªğ˜¯, ğ’™ = [ğ“Ÿ]ğ’š, using the LSQR method.
!!
!! LSQR is algebraically equivalent to applying CG
!! to the normal equations: (ğ“[ğ“Ÿ])*ğ“[ğ“Ÿ]ğ’š = (ğ“[ğ“Ÿ])*ğ’ƒ, ğ’™ = [ğ“Ÿ]ğ’š, (*)
!! (or, equivalently, [ğ“Ÿ*]ğ“*ğ“[ğ“Ÿ]ğ’š = [ğ“Ÿ*]ğ“*ğ’ƒ, ğ’™ = [ğ“Ÿ]ğ’š),
!! but but has better numerical properties.
!!
!! The residual norm â€–ğ“[ğ“Ÿ]ğ’š - ğ’ƒâ€–â‚‚ decreases monotonically, 
!! while the normal equation's residual norm â€–(ğ“[ğ“Ÿ])*(ğ“[ğ“Ÿ]ğ’š - ğ’ƒ)â€– 
!! is not guaranteed to decrease.
!! -=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=- !! 
subroutine Solve_LSQR(mesh, x, b, MatVec, &
    & ConjMatVec, params, Precond, ConjPrecond)
  class(tMesh), intent(inout) :: mesh
  real(dp), intent(in) :: b(:,:)
  real(dp), intent(inout) :: x(:,:)
  procedure(tMatVecFuncR) :: MatVec, ConjMatVec
  class(tConvParams), intent(inout) :: params
  procedure(tPrecondFuncR), optional :: Precond, ConjPrecond
  
  ! ----------------------
  ! [1] Paige, C. and M. Saunders. 
  !     â€œLSQR: An Algorithm for Sparse Linear Equations and Sparse Least Squares.â€ 
  !     ACM Trans. Math. Softw. 8 (1982): 43-71.
  ! [2] Karimi, S., D. K. Salkuyeh and F. Toutounian. 
  !     â€œA preconditioner for the LSQR algorithm.â€ 
  !     Journal of applied mathematics & informatics 26 (2008): 213-222.
  ! ----------------------

  real(dp) :: alpha, beta, rho, rho_bar, &
    & theta, phi, phi_bar, phi_tilde, cs, sn
  real(dp), pointer :: s(:,:), t(:,:), &
    & r(:,:), u(:,:), v(:,:), w(:,:), z(:,:)
  class(*), allocatable :: precond_env, precond_env_T
  
  allocate(t, r, u, v, w, z, mold=x)
  if (present(Precond)) then
    allocate(s, mold=x)
  end if

  ! ----------------------
  ! Utilize the initial guess.
  ! Consider the decomposition:
  ! ğ’™ = ğ’™â‚€ + ğ’›. (*)
  ! Substituting (*) into the equation, we get:
  ! ğ“[ğ“Ÿ]ğ’š = ğ’“, where: ğ’› = [ğ“Ÿ]ğ’š, ğ’“ = ğ’ƒ - ğ“ğ’™â‚€.
  ! The last equations can be solved with ğ’šâ‚€ = {0}áµ€.
  ! ----------------------

  ! ----------------------
  ! Initialize:
  ! ğ’“ â† ğ“ğ’™,
  ! ğ’“ â† ğ’ƒ - ğ’“,
  ! ğ›½ â† â€–ğ’“â€–, ğ’– â† ğ’“/ğ›½,
  ! ğ—¶ğ—³ ğ“Ÿ â‰  ğ—»ğ—¼ğ—»ğ—²: 
  !   ğ’” â† ğ“*ğ’–, ğ’• â† ğ“Ÿ*ğ’”, 
  ! ğ—²ğ—¹ğ˜€ğ—²: ğ’• â† ğ“*ğ’–, ğ—²ğ—»ğ—± ğ—¶ğ—³
  ! ğ›¼ â† â€–ğ’•â€–, ğ’— â† ğ’•/Î±.
  ! ----------------------
  call MatVec(mesh, r, x)
  call Sub(mesh, r, b, r)
  beta = Norm_2(mesh, r); call Scale(mesh, u, r, 1.0_dp/beta)
  if (present(Precond)) then
    call ConjMatVec(mesh, s, u)
    call ConjPrecond(mesh, t, s, ConjMatVec, precond_env_T)
  else
    call ConjMatVec(mesh, t, u)
  end if
  alpha = Norm_2(mesh, t); call Scale(mesh, v, t, 1.0_dp/alpha)

  ! ----------------------
  ! ğœ‘Ì… â† ğ›½, ğœŒÌ… â† ğ›¼.
  ! ğ’› â† {0}áµ€,
  ! ğ’˜ â† ğ’—,
  ! ----------------------
  phi_bar = beta; rho_bar = alpha
  call Fill(mesh, z, 0.0_dp)
  call Set(mesh, w, v)

  ! ----------------------
  ! ğœ‘Ìƒ â† ğœ‘Ì…,
  ! Check convergence for ğœ‘Ìƒ.
  ! ----------------------
  phi_tilde = phi_bar
  if (params%Check(phi_tilde)) return
  
  do
    ! ----------------------
    ! Continue the bidiagonalization:
    ! ğ—¶ğ—³ ğ“Ÿ â‰  ğ—»ğ—¼ğ—»ğ—²: 
    !   ğ’” â† ğ“Ÿğ’—, ğ’• â† ğ“ğ’”,
    ! ğ—²ğ—¹ğ˜€ğ—²: ğ’• â† ğ“ğ’—, ğ—²ğ—»ğ—± ğ—¶ğ—³
    ! ğ’• â† ğ’• - ğ›¼ğ’–,
    ! ğ›½ â† â€–ğ’•â€–, ğ’– â† ğ’•/ğ›½,
    ! ğ—¶ğ—³ ğ“Ÿ â‰  ğ—»ğ—¼ğ—»ğ—²:
    !   ğ’” â† ğ“*ğ’–, ğ’• â† ğ“Ÿ*ğ’”, 
    ! ğ—²ğ—¹ğ˜€ğ—²: ğ’• â† ğ“*ğ’–, ğ—²ğ—»ğ—± ğ—¶ğ—³
    ! ğ’• â† ğ’• - ğ›½ğ’—,
    ! ğ›¼ â† â€–ğ’•â€–, ğ’— â† ğ’•/ğ›¼.
    ! ----------------------
    if (present(Precond)) then
      call Precond(mesh, s, v, MatVec, precond_env)
      call MatVec(mesh, t, s)
    else
      call MatVec(mesh, t, v)
    end if
    call Sub(mesh, t, t, u, alpha)
    beta = Norm_2(mesh, t); call Scale(mesh, u, t, 1.0_dp/beta)
    if (present(Precond)) then
      call ConjMatVec(mesh, s, u)
      call ConjPrecond(mesh, t, s, ConjMatVec, precond_env_T)
    else
      call ConjMatVec(mesh, t, u)
    end if
    call Sub(mesh, t, t, v, beta)
    alpha = Norm_2(mesh, t); call Scale(mesh, v, t, 1.0_dp/alpha)
    
    ! ----------------------
    ! Construct and apply rotation:
    ! ğœŒ â† (ğœŒÌ…Â² + ğ›½Â²)Â¹áŸÂ²,
    ! ğ‘ğ‘  â† ğœŒÌ…/ğœŒ, ğ‘ ğ‘› â† ğ›½/ğœŒ,
    ! ğœƒ â† ğ‘ ğ‘›â‹…ğ›¼, ğœŒÌ… â† -ğ‘ğ‘ â‹…ğ›¼,
    ! ğœ‘ â† ğ‘ğ‘ â‹…ğœ‘, ğœ‘Ì… â† ğ‘ ğ‘›â‹…ğœ‘Ì….
    ! ----------------------
    rho = hypot(rho_bar, beta)
    cs = rho_bar/rho; sn = beta/rho
    theta = sn*alpha; rho_bar = -cs*alpha
    phi = cs*phi_bar; phi_bar = sn*phi_bar

    ! ----------------------
    ! Update ğ’›-solution:
    ! ğ’› â† ğ’› + (ğœ‘/ğœŒ)ğ’˜,
    ! ğ’˜ â† ğ’— - (ğœƒ/ğœŒ)ğ’˜.
    ! Check convergence for ğœ‘Ì… and ğœ‘Ì…/ğœ‘Ìƒ.
    ! ( ğœ‘Ì… and ğœ‘Ìƒ implicitly contain residual norms;
    !   ğœ‘Ì…|ğœŒÌ…| implicitly contain (ğ“[ğ“Ÿ])*-residual norms, â€–(ğ“[ğ“Ÿ])*ğ’“â€–. )
    ! ----------------------
    call Add(mesh, z, z, w, phi/rho)
    call Sub(mesh, w, v, w, theta/rho)
    if (params%Check(phi_bar, phi_bar/phi_tilde)) exit
  end do

  ! ----------------------
  ! Compute ğ’™-solution:
  ! ğ—¶ğ—³ ğ“Ÿ â‰  ğ—»ğ—¼ğ—»ğ—²:
  !   ğ’• â† ğ“Ÿğ’›, ğ’™ â† ğ’™ + ğ’•.
  ! ğ—²ğ—¹ğ˜€ğ—²: ğ’™ â† ğ’™ + ğ’›. ğ—²ğ—»ğ—± ğ—¶ğ—³
  ! ----------------------
  if (present(Precond)) then
    call Precond(mesh, t, z, MatVec, precond_env)
    call Add(mesh, x, x, t)
  else
    call Add(mesh, x, x, z)
  end if

end subroutine Solve_LSQR

!! -=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=- !! 
!! Solve a right preconditioned linear self-adjoint: least squares
!! problem: â€–ğ“[ğ“Ÿ]ğ’š - ğ’ƒâ€–â‚‚ â†’ ğ˜®ğ˜ªğ˜¯, ğ’™ = [ğ“Ÿ]ğ’š, using the LSQR method.
!!
!! LSQR is not recommended in the self-adjoint case,
!! please consider MINRES instead.
!! -=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=- !! 
subroutine Solve_SymmLSQR(mesh, x, b, MatVec, params, Precond)
  class(tMesh), intent(inout) :: mesh
  real(dp), intent(in) :: b(:,:)
  real(dp), intent(inout) :: x(:,:)
  procedure(tMatVecFuncR) :: MatVec
  class(tConvParams), intent(inout) :: params
  procedure(tPrecondFuncR), optional :: Precond

  if (present(Precond)) then
    call Solve_LSQR(mesh, x, b, MatVec, MatVec, params, Precond, Precond)
  else
    call Solve_LSQR(mesh, x, b, MatVec, MatVec, params)
  end if
  
end subroutine Solve_SymmLSQR

!! -=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=- !! 
!! Solve a right preconditioned linear least squares problem:
!! â€–ğ“[ğ“Ÿ]ğ’š - ğ’ƒâ€–â‚‚ â†’ ğ˜®ğ˜ªğ˜¯, ğ’™ = [ğ“Ÿ]ğ’š, using the LSMR method.
!!
!! LSMR is algebraically equivalent to applying MINRES 
!! to the normal equations: (ğ“[ğ“Ÿ])*ğ“[ğ“Ÿ]ğ’š = (ğ“[ğ“Ÿ])*ğ’ƒ, ğ’™ = [ğ“Ÿ]ğ’š, 
!! (or, equivalently, [ğ“Ÿ*]ğ“*ğ“[ğ“Ÿ]ğ’š = [ğ“Ÿ*]ğ“*ğ’ƒ, ğ’™ = [ğ“Ÿ]ğ’š),
!! but but has better numerical properties.
!! 
!! The normal equation's residual norm â€–(ğ“[ğ“Ÿ])*(ğ“[ğ“Ÿ]ğ’š - ğ’ƒ)â€– 
!! decreases monotonically, while the residual norm â€–ğ“[ğ“Ÿ]ğ’š - ğ’ƒâ€–â‚‚   
!! is not guaranteed to decrease (but decreases on practice).
!! -=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=- !! 
subroutine Solve_LSMR(mesh, x, b, MatVec, &
    & ConjMatVec, params, Precond, ConjPrecond)
  class(tMesh), intent(inout) :: mesh
  real(dp), intent(in) :: b(:,:)
  real(dp), intent(inout) :: x(:,:)
  procedure(tMatVecFuncR) :: MatVec, ConjMatVec
  class(tConvParams), intent(inout) :: params
  procedure(tPrecondFuncR), optional :: Precond, ConjPrecond
  
  ! ----------------------
  ! [1] Fong, D. C. and M. Saunders. 
  !     â€œLSMR: An Iterative Algorithm for Sparse Least-Squares Problems.â€ 
  !     SIAM J. Sci. Comput. 33 (2011): 2950-2971.
  ! [2] Karimi, S., D. K. Salkuyeh and F. Toutounian. 
  !     â€œA preconditioner for the LSQR algorithm.â€ 
  !     Journal of applied mathematics & informatics 26 (2008): 213-222.
  ! ----------------------

  real(dp) :: alpha, alpha_bar, beta, rho, rho_bar, &
    & theta, theta_bar, psi, psi_bar, psi_tilde, zeta, &
    & cs, sn, cs_bar, sn_bar
  real(dp), pointer :: r(:,:), s(:,:), t(:,:), &
    & w(:,:), h(:,:), u(:,:), v(:,:), z(:,:)
  class(*), allocatable :: precond_env, precond_env_T
  
  allocate(t, r, u, v, w, h, z, mold=x)
  if (present(Precond)) then
    allocate(s, mold=x)
  end if

  ! ----------------------
  ! Utilize the initial guess.
  ! Consider the decomposition:
  ! ğ’™ = ğ’™â‚€ + ğ’›. (*)
  ! Substituting (*) into the equation, we get:
  ! ğ“[ğ“Ÿ]ğ’š = ğ’“, where: ğ’› = [ğ“Ÿ]ğ’š, ğ’“ = ğ’ƒ - ğ“ğ’™â‚€.
  ! The last equations can be solved with ğ’šâ‚€ = {0}áµ€.
  ! ----------------------

  ! ----------------------
  ! Initialize:
  ! ğ’“ â† ğ“ğ’™,
  ! ğ’“ â† ğ’ƒ - ğ’“,
  ! ğ›½ â† â€–ğ’“â€–, ğ’– â† ğ’“/ğ›½,
  ! ğ—¶ğ—³ ğ“Ÿ â‰  ğ—»ğ—¼ğ—»ğ—²: 
  !   ğ’” â† ğ“*ğ’–, ğ’• â† ğ“Ÿ*ğ’”, 
  ! ğ—²ğ—¹ğ˜€ğ—²: ğ’• â† ğ“*ğ’–, ğ—²ğ—»ğ—± ğ—¶ğ—³
  ! ğ›¼ â† â€–ğ’•â€–, ğ’— â† ğ’•/Î±.
  ! ----------------------
  call MatVec(mesh, r, x)
  call Sub(mesh, r, b, r)
  beta = Norm_2(mesh, r); call Scale(mesh, u, r, 1.0_dp/beta)
  if (present(Precond)) then
    call ConjMatVec(mesh, s, u)
    call ConjPrecond(mesh, t, s, ConjMatVec, precond_env_T)
  else
    call ConjMatVec(mesh, t, u)
  end if
  alpha = Norm_2(mesh, t); call Scale(mesh, v, t, 1.0_dp/alpha)

  ! ----------------------
  ! ğ›¼Ì… â† ğ›¼, ğœ“Ì… â† ğ›¼ğ›½,
  ! ğœ â† 1, ğ‘Ì…ğ‘ Ì… â† 1, ğ‘ Ì…ğ‘›Ì… â† 0,
  ! ğ’› â† {0}áµ€,
  ! ğ’˜ â† ğ’—, ğ’‰ â† {0}áµ€.
  ! ----------------------
  alpha_bar = alpha; psi_bar = alpha*beta
  zeta = 1.0_dp; cs_bar = 1.0_dp; sn_bar = 0.0_dp
  call Fill(mesh, z, 0.0_dp)
  call Set(mesh, w, v); call Fill(mesh, h, 0.0_dp)

  ! ----------------------
  ! ğœ“Ìƒ â† ğœ“Ì…,
  ! Check convergence for ğœ“Ìƒ.
  ! ----------------------
  psi_tilde = psi_bar
  if (params%Check(psi_tilde)) return
  
  do
    ! ----------------------
    ! Continue the bidiagonalization:
    ! ğ—¶ğ—³ ğ“Ÿ â‰  ğ—»ğ—¼ğ—»ğ—²: 
    !   ğ’” â† ğ“Ÿğ’—, ğ’• â† ğ“ğ’”,
    ! ğ—²ğ—¹ğ˜€ğ—²: ğ’• â† ğ“ğ’—, ğ—²ğ—»ğ—± ğ—¶ğ—³
    ! ğ’• â† ğ’• - ğ›¼ğ’–,
    ! ğ›½ â† â€–ğ’•â€–, ğ’– â† ğ’•/ğ›½,
    ! ğ—¶ğ—³ ğ“Ÿ â‰  ğ—»ğ—¼ğ—»ğ—²:
    !   ğ’” â† ğ“*ğ’–, ğ’• â† ğ“Ÿ*ğ’”, 
    ! ğ—²ğ—¹ğ˜€ğ—²: ğ’• â† ğ“*ğ’–, ğ—²ğ—»ğ—± ğ—¶ğ—³
    ! ğ’• â† ğ’• - ğ›½ğ’—,
    ! ğ›¼ â† â€–ğ’•â€–, ğ’— â† ğ’•/ğ›¼.
    ! ----------------------
    if (present(Precond)) then
      call Precond(mesh, s, v, MatVec, precond_env)
      call MatVec(mesh, t, s)
    else
      call MatVec(mesh, t, v)
    end if
    call Sub(mesh, t, t, u, alpha)
    beta = Norm_2(mesh, t); call Scale(mesh, u, t, 1.0_dp/beta)
    if (present(Precond)) then
      call ConjMatVec(mesh, s, u)
      call ConjPrecond(mesh, t, s, ConjMatVec, precond_env_T)
    else
      call ConjMatVec(mesh, t, u)
    end if
    call Sub(mesh, t, t, v, beta)
    alpha = Norm_2(mesh, t); call Scale(mesh, v, t, 1.0_dp/alpha)
    
    ! ----------------------
    ! Construct and apply rotations:
    ! ğœŒ â† (ğ›¼Ì…Â² + ğ›½Â²)Â¹áŸÂ²,
    ! ğ‘ğ‘  â† ğ›¼Ì…/ğœŒ, ğ‘ ğ‘› â† ğ›½/ğœŒ,
    ! ğœƒ â† ğ‘ ğ‘›â‹…ğ›¼, ğ›¼Ì… â† ğ‘ğ‘ â‹…ğ›¼,
    ! ğœƒÌ… â† ğ‘ Ì…ğ‘›Ì…â‹…ğœŒ, ğœŒÌ… â† [(ğ‘Ì…ğ‘ Ì…â‹…ğœŒ)Â² + ğœƒÂ²]Â¹áŸÂ²,
    ! ğ‘Ì…ğ‘ Ì… â† ğ‘Ì…ğ‘ Ì…â‹…ğœŒ/ğœŒÌ…, ğ‘ Ì…ğ‘›Ì… â† ğœƒ/ğœŒÌ…,
    ! ğœ“ â† ğ‘Ì…ğ‘ Ì…â‹…ğœ“Ì…, ğœ“Ì… â† -ğ‘ Ì…ğ‘›Ì…â‹…ğœ“Ì….
    ! ----------------------
    rho = hypot(alpha_bar, beta)
    cs = alpha_bar/rho; sn = beta/rho
    theta = sn*alpha; alpha_bar = cs*alpha
    theta_bar = sn_bar*rho; rho_bar = hypot(cs_bar*rho, theta)
    cs_bar = cs_bar*rho/rho_bar; sn_bar = theta/rho_bar
    psi = cs_bar*psi_bar; psi_bar = -sn_bar*psi_bar

    ! ----------------------
    ! Update ğ’›-solution:
    ! ğ’‰ â† ğ’˜ - (ğœƒğœŒ/ğœ)ğ’‰,
    ! ğœ â† ğœŒğœŒÌ…,
    ! ğ’› â† ğ’› + (ğœ“/ğœ)ğ’‰,
    ! ğ’˜ â† ğ’— - (ğœƒ/ğœŒ)ğ’˜.
    ! Check convergence for |ğœ“Ì…| and |ğœ“Ì…/ğœ“Ìƒ|.
    ! ( |ğœ“Ì…| and |ğœ“Ìƒ| implicitly contain (ğ“[ğ“Ÿ])*-residual norms, â€–(ğ“[ğ“Ÿ])*ğ’“â€–. )
    ! ----------------------
    call Sub(mesh, h, w, h, theta_bar*rho/zeta)
    zeta = rho*rho_bar
    call Add(mesh, z, z, h, psi/zeta)
    call Sub(mesh, w, v, w, theta/rho)
    if (params%Check(abs(psi_bar), abs(psi_bar/psi_tilde))) exit
  end do

  ! ----------------------
  ! Compute ğ’™-solution:
  ! ğ—¶ğ—³ ğ“Ÿ â‰  ğ—»ğ—¼ğ—»ğ—²:
  !   ğ’• â† ğ“Ÿğ’›, ğ’™ â† ğ’™ + ğ’•.
  ! ğ—²ğ—¹ğ˜€ğ—²: ğ’™ â† ğ’™ + ğ’›. ğ—²ğ—»ğ—± ğ—¶ğ—³
  ! ----------------------
  if (present(Precond)) then
    call Precond(mesh, t, z, MatVec, precond_env)
    call Add(mesh, x, x, t)
  else
    call Add(mesh, x, x, z)
  end if

end subroutine Solve_LSMR

!! -=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=- !! 
!! Solve a right preconditioned linear self-adjoint: least squares
!! problem: â€–ğ“[ğ“Ÿ]ğ’š - ğ’ƒâ€–â‚‚ â†’ ğ˜®ğ˜ªğ˜¯, ğ’™ = [ğ“Ÿ]ğ’š, using the LSMR method.
!!
!! Using LSMR is not recommended in the self-adjoint case,
!! please consider MINRES instead.
!! -=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=- !! 
subroutine Solve_SymmLSMR(mesh, x, b, MatVec, params, Precond)
  class(tMesh), intent(inout) :: mesh
  real(dp), intent(in) :: b(:,:)
  real(dp), intent(inout) :: x(:,:)
  procedure(tMatVecFuncR) :: MatVec
  class(tConvParams), intent(inout) :: params
  procedure(tPrecondFuncR), optional :: Precond

  ! ----------------------
  ! Using LSMR in symmetric case is not recommended,
  ! use MINRES instead.
  ! ----------------------

  if (present(Precond)) then
    call Solve_LSMR(mesh, x, b, MatVec, MatVec, params, Precond, Precond)
  else
    call Solve_LSMR(mesh, x, b, MatVec, MatVec, params)
  end if
  
end subroutine Solve_SymmLSMR

end module StormRuler_Solvers_LSQR
