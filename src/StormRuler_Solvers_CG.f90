!! <<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<< !!
!! Copyright (C) 2021 Oleg Butakov
!!
!! Permission is hereby granted, free of charge, to any person
!! obtaining a copy of this software and associated documentation
!! files (the "Software"), to deal in the Software without
!! restriction, including without limitation the rights  to use,
!! copy, modify, merge, publish, distribute, sublicense, and/or
!! sell copies of the Software, and to permit persons to whom the 
!! Software is furnished to do so, subject to the following
!! conditions:
!!
!! The above copyright notice and this permission notice shall be
!! included in all copies or substantial portions of the Software.
!!
!! THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND,
!! EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES
!! OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND
!! NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT
!! HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY,
!! WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING
!! FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR
!! OTHER DEALINGS IN THE SOFTWARE.
!! >>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>> !!
module StormRuler_Solvers_CG

#$use 'StormRuler_Params.fi'

use StormRuler_Parameters, only: dp
use StormRuler_Helpers, only: SafeDivide

use StormRuler_Mesh, only: tMesh
use StormRuler_Array, only: tArray, AllocArray

use StormRuler_BLAS, only: Fill, Set, Dot, Add, Sub
use StormRuler_BLAS, only: tMatVecFunc

use StormRuler_ConvParams, only: tConvParams
use StormRuler_Preconditioner, only: tPreconditioner

!! <<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<< !!
!! >>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>> !!

implicit none

interface Solve_CG
  module procedure Solve_CG
end interface Solve_CG

interface Solve_BiCGStab
  module procedure Solve_BiCGStab
end interface Solve_BiCGStab

!! <<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<< !!
!! >>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>> !!

contains

!! -=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=- !!
!! Solve a linear self-adjoint definite operator equation: 
!! [ğ“œ]ğ“[ğ“œáµ€]ğ’š = [ğ“œ]ğ’ƒ, ğ’™ = [ğ“œáµ€]ğ’š, [ğ“œğ“œáµ€ = ğ“Ÿ], using the 
!! Conjugate Gradients (CG) method.
!!
!! CG may be applied to the consistent singular problems, 
!! it converges towards..
!! -=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=- !! 
subroutine Solve_CG(mesh, xArr, bArr, MatVec, params, pre)
  class(tMesh), intent(in) :: mesh
  class(tArray), intent(in) :: bArr
  class(tArray), intent(inout) :: xArr
  class(tConvParams), intent(inout) :: params
  class(tPreconditioner), intent(inout), optional :: pre
  procedure(tMatVecFunc) :: MatVec
  
  real(dp) :: alpha, beta, gamma, delta
  type(tArray) :: pArr, rArr, tArr, zArr
  
  call AllocArray(pArr, rArr, tArr, mold=xArr)
  if (present(pre)) then
    call AllocArray(zArr, mold=xArr)
    call pre%Init(mesh, MatVec)
  else
    zArr = rArr
  end if

  ! ----------------------
  ! ğ’“ â† ğ“ğ’™,
  ! ğ’“ â† ğ’ƒ - ğ’•.
  ! ----------------------
  call MatVec(mesh, rArr, xArr)
  call Sub(mesh, rArr, bArr, rArr)

  ! ----------------------
  ! ğ›¿ â† <ğ’“â‹…ğ’“>,
  ! Check convergence for âˆšğ›¿.
  ! ----------------------
  delta = Dot(mesh, rArr, rArr)
  if (params%Check(sqrt(delta))) return
  
  ! ----------------------
  ! ğ’› â† ğ“Ÿğ’“,
  ! ğ’‘ â† ğ’›,
  ! ğ›¾ â† <ğ’“â‹…ğ’›>,
  ! ----------------------
  if (present(pre)) then
    call pre%Apply(mesh, zArr, rArr, MatVec)
  end if
  call Set(mesh, pArr, zArr)
  gamma = Dot(mesh, rArr, zArr)

  do
    ! ----------------------
    ! ğ’• â† ğ“ğ’‘,
    ! ğ›¼ â† ğ›¾/<ğ’‘â‹…ğ’•>,
    ! ğ’™ â† ğ’™ + ğ›¼ğ’‘,
    ! ğ’“ â† ğ’“ - ğ›¼ğ’•,
    ! ----------------------
    call MatVec(mesh, tArr, pArr)
    alpha = SafeDivide(gamma, Dot(mesh, pArr, tArr))
    call Add(mesh, xArr, xArr, pArr, alpha)
    call Sub(mesh, rArr, rArr, tArr, alpha)

    ! ----------------------
    ! ğ›¼ â† <ğ’“â‹…ğ’“>,
    ! Check convergence for âˆšğ›¼ and âˆšğ›¼/âˆšğ›¿.
    ! ----------------------
    alpha = Dot(mesh, rArr, rArr)
    if (params%Check(sqrt(alpha), sqrt(alpha/delta))) exit

    ! ----------------------
    ! ğ—¶ğ—³ ğ“Ÿ â‰  ğ—»ğ—¼ğ—»ğ—²:
    !   ğ’› â† ğ“Ÿğ’“,
    !   ğ›¼ â† <ğ’“â‹…ğ’›>,
    ! ğ—²ğ—»ğ—± ğ—¶ğ—³ // otherwise ğ’› â‰¡ ğ’“, ğ›¼ unchanged.  
    ! ----------------------
    if (present(pre)) then
      call pre%Apply(mesh, zArr, rArr, MatVec)
      alpha = Dot(mesh, rArr, zArr)
    end if

    ! ----------------------
    ! ğ›½ â† ğ›¼/ğ›¾,
    ! ğ’‘ â† ğ’› + ğ›½ğ’‘,
    ! ğ›¾ â† ğ›¼.
    ! ----------------------
    beta = SafeDivide(alpha, gamma)
    call Add(mesh, pArr, zArr, pArr, beta)
    gamma = alpha

  end do

end subroutine Solve_CG

!! -=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=- !! 
!! Solve a linear operator equation: [ğ“Ÿ]ğ“ğ’™ = [ğ“Ÿ]ğ’ƒ, using 
!! the good old Biconjugate Gradients (stabilized) method (BiCGStab).
!!
!! BiCGStab may be applied to the consistent singular problems,
!! it converges towards..
!! -=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=- !! 
subroutine Solve_BiCGStab(mesh, xArr, bArr, MatVec, params, pre)
  class(tMesh), intent(in) :: mesh
  class(tArray), intent(in) :: bArr
  class(tArray), intent(inout) :: xArr
  class(tConvParams), intent(inout) :: params
  class(tPreconditioner), intent(inout), optional :: pre
  procedure(tMatVecFunc) :: MatVec

  real(dp) :: alpha, beta, gamma, delta, mu, rho, omega
  type(tArray) :: pArr, rArr, rTildeArr, sArr, tArr, vArr, wArr, yArr, zArr

  call AllocArray(pArr, rArr, rTildeArr, sArr, tArr, vArr, mold=xArr)
  if (present(pre)) then
    call AllocArray(wArr, yArr, zArr, mold=xArr)
    call pre%Init(mesh, MatVec)
  else
    wArr = tArr; yArr = pArr; zArr = sArr
  end if

  ! ----------------------
  ! ğ’“ â† ğ“ğ’™,
  ! ğ’“ â† ğ’ƒ - ğ’“.
  ! ----------------------
  call MatVec(mesh, rArr, xArr)
  call Sub(mesh, rArr, bArr, rArr)

  ! ----------------------
  ! ğ›¿ â† <ğ’“â‹…ğ’“>,
  ! Check convergence for âˆšğ›¿.
  ! ----------------------
  delta = Dot(mesh, rArr, rArr)
  if (params%Check(sqrt(delta))) return
  
  ! ----------------------
  ! ğ’“Ìƒ â† ğ’“,
  ! ğ’‘ â† {ğŸ¢}áµ€, ğ’— â† {ğŸ¢}áµ€,
  ! ğœŒ â† ğŸ£, ğ›¼ â† ğŸ£, ğœ” â† ğŸ£.
  ! ----------------------
  call Set(mesh, rTildeArr, rArr)
  call Fill(mesh, pArr, 0.0_dp)
  call Fill(mesh, vArr, 0.0_dp)
  rho = 1.0_dp; alpha = 1.0_dp; omega = 1.0_dp

  do
    ! ----------------------
    ! ğœ‡ â† <ğ’“Ìƒâ‹…ğ’“>,
    ! ğ›½ â† (ğœ‡/ğœŒ)â‹…(ğ›¼/ğœ”),
    ! ğœŒ â† ğœ‡.
    ! ----------------------
    mu = Dot(mesh, rTildeArr, rArr)
    beta = SafeDivide(mu, rho)*SafeDivide(alpha, omega)
    rho = mu
    
    ! ----------------------
    ! ğ’‘ â† ğ’‘ - ğœ”ğ’—,
    ! ğ’‘ â† ğ’“ + ğ›½ğ’‘,
    ! ğ’š â† ğ“Ÿğ’‘,
    ! ğ’— â† ğ“ğ’š.
    ! ----------------------
    call Sub(mesh, pArr, pArr, vArr, omega)
    call Add(mesh, pArr, rArr, pArr, beta)
    if (present(pre)) then
      call pre%Apply(mesh, yArr, pArr, MatVec)
    end if
    call MatVec(mesh, vArr, yArr)
    
    ! ----------------------
    ! ğ›¼ â† ğœŒ/<ğ’“Ìƒâ‹…ğ’—>,
    ! ğ’” â† ğ’“ - ğ›¼ğ’—,
    ! ğ’› â† ğ“Ÿğ’”,
    ! ğ’• â† ğ“ğ’›.
    ! ----------------------
    alpha = SafeDivide(rho, Dot(mesh, rTildeArr, vArr))
    call Sub(mesh, sArr, rArr, vArr, alpha)
    if (present(pre)) then
      call pre%Apply(mesh, zArr, sArr, MatVec)
    end if
    call MatVec(mesh, tArr, zArr)
    
    ! ----------------------
    ! ğ’˜ â† ğ“Ÿğ’•,
    ! ğœ” â† <ğ’˜â‹…ğ’›>/<ğ’˜â‹…ğ’˜>,
    ! ğ’“ â† ğ’” - ğœ”ğ’•,
    ! ğ’™ â† ğ’™ + ğœ”ğ’›,
    ! ğ’™ â† ğ’™ + ğ›¼ğ’š,
    ! ----------------------
    if (present(pre)) then
      call pre%Apply(mesh, wArr, tArr, MatVec)
    end if
    omega = SafeDivide(Dot(mesh, wArr, zArr), Dot(mesh, wArr, wArr))
    call Sub(mesh, rArr, sArr, tArr, omega)
    call Add(mesh, xArr, xArr, zArr, omega)
    call Add(mesh, xArr, xArr, yArr, alpha)
    
    ! ----------------------
    ! ğ›¾ â† <ğ’“â‹…ğ’“>,
    ! Check convergence for âˆšğ›¾ and âˆšğ›¾/âˆšğ›¿.
    ! ----------------------
    gamma = Dot(mesh, rArr, rArr)
    if (params%Check(sqrt(gamma), sqrt(gamma/delta))) exit

  end do

end subroutine Solve_BiCGStab

end module StormRuler_Solvers_CG
