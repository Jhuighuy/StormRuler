!! <<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<< !!
!! Copyright (C) 2021 Oleg Butakov
!!
!! Permission is hereby granted, free of charge, to any person
!! obtaining a copy of this software and associated documentation
!! files (the "Software"), to deal in the Software without
!! restriction, including without limitation the rights  to use,
!! copy, modify, merge, publish, distribute, sublicense, and/or
!! sell copies of the Software, and to permit persons to whom the 
!! Software is furnished to do so, subject to the following
!! conditions:
!!
!! The above copyright notice and this permission notice shall be
!! included in all copies or substantial portions of the Software.
!!
!! THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND,
!! EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES
!! OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND
!! NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT
!! HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY,
!! WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING
!! FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR
!! OTHER DEALINGS IN THE SOFTWARE.
!! >>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>> !!
module StormRuler_Solvers_MINRES

use StormRuler_Consts, only: ip, dp
use StormRuler_Parameters, only: gMaxIterGMRES

use StormRuler_Mesh, only: tMesh
use StormRuler_Array, only: tArray, AllocArray

use StormRuler_BLAS, only: Dot, Norm_2, Fill, Set, Scale, Add, Sub
use StormRuler_BLAS, only: tMatVecFunc

use StormRuler_ConvParams, only: tConvParams
use StormRuler_Preconditioner, only: tPreconditioner

!! <<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<< !!
!! >>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>> !!

implicit none

interface Solve_MINRES
  module procedure Solve_MINRES
end interface Solve_MINRES

interface Solve_GMRES
  module procedure Solve_GMRES
end interface Solve_GMRES

interface Solve_TFQMR
  module procedure Solve_TFQMR
end interface Solve_TFQMR

!! <<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<< !!
!! >>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>> !!

contains

!! ----------------------------------------------------------------- !!
!! Generate Givens rotation.
!! ----------------------------------------------------------------- !!
subroutine SymOrtho(a, b, cs, sn, rr)
  real(dp), intent(in) :: a, b
  real(dp), intent(out) :: cs, sn, rr

  ! ----------------------
  ! ğ‘Ÿğ‘Ÿ â† (ğ‘Â² + ğ‘Â²)Â¹áŸÂ²,
  ! ğ‘ğ‘  â† ğ‘/ğ‘Ÿğ‘Ÿ, ğ‘ ğ‘› â† ğ‘/ğ‘Ÿğ‘Ÿ. 
  ! ----------------------
  rr = hypot(a, b)
  if (rr > 0.0_dp) then
    cs = a/rr; sn = b/rr
  else
    cs = 1.0_dp; sn = 0.0_dp
  end if

end subroutine SymOrtho

!! -=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=- !! 
!! Solve a linear self-adjoint indefinite operator equation: 
!! [ğ“œ]ğ“[ğ“œáµ€]ğ’š = [ğ“œ]ğ’ƒ, [ğ“œáµ€]ğ’š = ğ’™, [ğ“œğ“œáµ€ = ğ“Ÿ], using the MINRES method.
!!
!! Despite ğ“ may be indefinite, a positive-definite preconditioner ğ“Ÿ 
!! is explicitly required.
!!
!! MINRES may be applied to the singular problems, and the self-adjoint
!! least squares problems: â€–[ğ“œ](ğ“[ğ“œáµ€]ğ’š - ğ’ƒ)â€–â‚‚ â†’ ğ˜®ğ˜ªğ˜¯, ğ’™ = [ğ“œáµ€]ğ’š, 
!! although convergeance to minimum norm solution is not guaranteed.
!! 
!! References:
!! [1] Paige, C. and M. Saunders. 
!!     â€œSolution of Sparse Indefinite Systems of Linear Equations.â€ 
!!     SIAM Journal on Numerical Analysis 12 (1975): 617-629.
!! [2] Choi, S.-C. T.
!!     â€œIterative Methods for Singular Linear Equations and 
!!     Least-Squares Problemsâ€ PhD thesis, ICME, Stanford University.
!! -=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=- !! 
subroutine Solve_MINRES(mesh, xArr, bArr, MatVec, params, pre)
  class(tMesh), intent(in) :: mesh
  class(tArray), intent(in) :: bArr
  class(tArray), intent(inout) :: xArr
  class(tConvParams), intent(inout) :: params
  class(tPreconditioner), intent(inout), optional :: pre
  procedure(tMatVecFunc) :: MatVec

  real(dp) :: alpha, beta, betaBar, gamma, delta, deltaBar, &
    & epsilon, epsilonBar, tau, phi, phiTilde, cs, sn
  type(tArray) :: tmp, pArr, qArr, qBarArr, &
    & wArr, wBar, wBarBar, zArr, zBarArr, zBarBarArr

  call AllocArray(pArr, wArr, wBar, wBarBar, zArr, zBarArr, zBarBarArr, mold=xArr)
  if (present(pre)) then
    call AllocArray(qArr, qBarArr, mold=xArr)
    call pre%Init(mesh, MatVec)
  end if

  ! ----------------------
  ! Initialize:
  ! ğ’˜Ì… â† {ğŸ¢}áµ€,
  ! ğ’˜Ì¿ â† {ğŸ¢}áµ€,
  ! ğ’›Ì… â† ğ“ğ’™,     // Modification in order to
  ! ğ’›Ì… â† ğ’ƒ - ğ’›Ì…,  // utilize the initial guess.
  ! ğ’›Ì¿ â† {ğŸ¢}áµ€,
  ! ğ’’ â† [ğ“Ÿ]ğ’›Ì…,
  ! ğ›½Ì… â† ğŸ£, ğ›½ â† âˆš<ğ’’â‹…ğ’›Ì…>,
  ! ğœ‘ â† ğ›½, ğ›¿ â† ğŸ¢, ğœ€ â† ğŸ¢,
  ! ğ‘ğ‘  â† -ğŸ£, ğ‘ ğ‘› â† ğŸ¢.
  ! ----------------------
  call Fill(mesh, wBar, 0.0_dp)
  call Fill(mesh, wBarBar, 0.0_dp)
  call MatVec(mesh, zBarArr, xArr)
  call Sub(mesh, zBarArr, bArr, zBarArr)
  call Fill(mesh, zBarBarArr, 0.0_dp)
  if (present(pre)) then
    call pre%Apply(mesh, qArr, zBarArr, MatVec)
  else
    qArr = zBarArr
  end if
  betaBar = 1.0_dp; beta = sqrt(Dot(mesh, qArr, zBarArr))
  phi = beta; delta = 0.0_dp; epsilon = 0.0_dp
  cs = -1.0_dp; sn = 0.0_dp

  ! ----------------------
  ! ğœ‘Ìƒ â† ğœ‘,
  ! Check convergence for ğœ‘Ìƒ.
  ! ----------------------
  phiTilde = phi
  if (params%Check(phiTilde)) return

  do
    ! ----------------------
    ! Continue the Lanczos process:
    ! ğ’‘ â† ğ“ğ’’,
    ! ğ›¼ â† <ğ’’â‹…ğ’‘>/ğ›½Â²,
    ! ğ’› â† (ğŸ£/ğ›½)ğ’‘ - (ğ›¼/ğ›½)ğ’›Ì…,
    ! ğ’› â† ğ’› - (ğ›½/ğ›½Ì…)ğ’›Ì¿,
    ! ğ’’Ì… â† ğ’’, ğ’’ â† [ğ“Ÿ]ğ’›,
    ! ğ›½Ì… â† ğ›½, ğ›½ â† âˆš<ğ’’â‹…ğ’›>,
    ! ğ’›Ì¿ â† ğ’›Ì…, ğ’›Ì… â† ğ’›.
    ! ----------------------
    call MatVec(mesh, pArr, qArr)
    alpha = Dot(mesh, qArr, pArr)/(beta**2)
    call Sub(mesh, zArr, pArr, zBarArr, alpha/beta, 1.0_dp/beta)
    call Sub(mesh, zArr, zArr, zBarBarArr, beta/betaBar)
    if (present(pre)) then
      tmp = qBarArr; qBarArr = qArr; qArr = tmp
      call pre%Apply(mesh, qArr, zArr, MatVec)
    else
      qBarArr = qArr; qArr = zArr
    end if
    betaBar = beta; beta = sqrt(Dot(mesh, qArr, zArr))
    tmp = zBarBarArr; zBarBarArr = zBarArr; zBarArr = zArr; zArr = tmp

    ! ----------------------
    ! Construct and apply rotations:
    ! ğ›¿Ì… â† ğ‘ğ‘ â‹…ğ›¿ + ğ‘ ğ‘›â‹…ğ›¼, ğ›¾ â† ğ‘ ğ‘›â‹…ğ›¿ - ğ‘ğ‘ â‹…ğ›¼,
    ! ğœ€Ì… â† ğœ€, ğœ€ â† ğ‘ ğ‘›â‹…ğ›½, ğ›¿ â† -ğ‘ğ‘ â‹…ğ›½,
    ! ğ‘ğ‘ , ğ‘ ğ‘›, ğ›¾ â† ğ˜šğ˜ºğ˜®ğ˜–ğ˜³ğ˜µğ˜©ğ˜°(ğ›¾,ğ›½),
    ! ğœ â† ğ‘ğ‘ â‹…ğœ‘, ğœ‘ â† ğ‘ ğ‘›â‹…ğœ‘.
    ! ----------------------
    deltaBar = cs*delta + sn*alpha; gamma = sn*delta - cs*alpha
    epsilonBar = epsilon; epsilon = sn*beta; delta = -cs*beta
    call SymOrtho(+gamma, beta, cs, sn, gamma)
    tau = cs*phi; phi = sn*phi
    
    ! ----------------------
    ! Update solution:
    ! ğ’˜ â† (ğŸ£/(ğ›½Ì…ğ›¾))ğ’’Ì… - (ğ›¿Ì…/ğ›¾)ğ’˜Ì…,
    ! ğ’˜ â† ğ’˜ - (ğœ€Ì…/ğ›¾)ğ’˜Ì¿,
    ! ğ’™ â† ğ’™ + ğœğ’˜,
    ! ğ’˜Ì¿ â† ğ’˜Ì…, ğ’˜Ì… â† ğ’˜.
    ! ----------------------
    call Sub(mesh, wArr, qBarArr, wBar, deltaBar/gamma, 1.0_dp/(betaBar*gamma))
    call Sub(mesh, wArr, wArr, wBarBar, epsilonBar/gamma)
    call Add(mesh, xArr, xArr, wArr, tau)
    tmp = wBarBar; wBarBar = wBar; wBar = wArr; wArr = tmp

    ! ----------------------
    ! Check convergence for ğœ‘ and ğœ‘/ğœ‘Ìƒ.
    ! ( ğœ‘ and ğœ‘Ìƒ implicitly contain residual norms. )
    ! ----------------------
    if (params%Check(phi, phi/phiTilde)) exit
  end do

end subroutine Solve_MINRES

!! -=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=- !! 
!! Solve a linear operator equation: ğ“[ğ“Ÿ]ğ’™ = ğ’ƒ, ğ’™ = [ğ“Ÿ]ğ’š, using 
!! the monstrous Generalized Minimal Residual method (GMRES).
!! 
!! The classical GMRES(ğ‘š) implementation with restarts
!! after ğ‘š iterations is used.
!! 
!! GMRES may be applied to the singular problems, and the square
!! least squares problems: â€–(ğ“[ğ“Ÿ]ğ’š - ğ’ƒ)â€–â‚‚ â†’ ğ˜®ğ˜ªğ˜¯, ğ’™ = [ğ“Ÿ]ğ’š, 
!! although convergeance to minimum norm solution is not guaranteed 
!! (is this true?).
!!
!! References:
!! [1] Saad and M.H. Schultz, 
!!     "GMRES: A generalized minimal residual algorithm for solving 
!!      nonsymmetric linear systems", 
!!     SIAM J. Sci. Stat. Comput., 7:856â€“869, 1986.
!! -=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=- !! 
subroutine Solve_GMRES(mesh, xArr, bArr, MatVec, params, pre)
  class(tMesh), intent(in) :: mesh
  class(tArray), intent(in) :: bArr
  class(tArray), intent(inout) :: xArr
  class(tConvParams), intent(inout) :: params
  class(tPreconditioner), intent(inout), optional :: pre
  procedure(tMatVecFunc) :: MatVec

  logical :: converged
  integer(ip) :: i, k
  real(dp) :: chi, phi, phiTilde
  real(dp), pointer :: beta(:), cs(:), sn(:), H(:,:)
  type(tArray) :: QArr, sArr, tArr, rArr

  call AllocArray(rArr, mold=xArr)
  associate(m => gMaxIterGMRES)
    call AllocArray(QArr, shape=[xArr%mShape, m + 1])
    allocate(beta(m + 1), cs(m), sn(m), H(m + 1,m))
  end associate

  if (present(pre)) then
    error stop 'preconditioned GMRES solver is not implemented.'
  end if

  ! ----------------------
  ! Pre-initialize:
  ! ğœ‘Ìƒ â† ğŸ¢,
  ! ğ˜¤ğ˜°ğ˜¯ğ˜·ğ˜¦ğ˜³ğ˜¨ğ˜¦ğ˜¥ â† ğ™›ğ™–ğ™¡ğ™¨ğ™š.
  ! ----------------------
  phiTilde = 0.0_dp
  converged = .false.

  do while(.not.converged)
    ! ----------------------
    ! Initialize:
    ! ğ’“ â† ğ“ğ’™,
    ! ğ’“ â† ğ’ƒ - ğ’“,
    ! ğœ‘ â† â€–ğ’“â€–,
    ! ğ—¶ğ—³ ğœ‘Ìƒ = ğŸ¢: // first non-restarted pass.
    !   ğœ‘Ìƒ â† ğœ‘,
    !   Check convergence for ğœ‘Ìƒ.
    ! ğ—²ğ—»ğ—± ğ—¶ğ—³
    ! ----------------------
    call MatVec(mesh, rArr, xArr)
    call Sub(mesh, rArr, bArr, rArr)
    phi = Norm_2(mesh, rArr)
    if (phiTilde <= 0.0) then
      phiTilde = phi
      if (params%Check(phiTilde)) return
    end if

    ! ----------------------
    ! ğ’„ğ’” â† {ğŸ¢}áµ€, ğ’”ğ’ â† {ğŸ¢}áµ€,
    ! ğœ· â† {ğœ‘,ğŸ¢,â€¦,ğŸ¢}áµ€,
    ! ğ“ â‚ â† ğ’“/ğœ‘. 
    ! ----------------------
    cs(:) = 0.0_dp; sn(:) = 0.0_dp
    beta(1) = phi; beta(2:) = 0.0_dp
    tArr = QArr%Slice(1); call Scale(mesh, tArr, rArr, 1.0_dp/phi)

    do k = 1, gMaxIterGMRES
      ! ----------------------
      ! Arnoldi iteration:
      ! ğ“ â‚–â‚Šâ‚ â† ğ“ğ“ â‚–,
      ! ğ—³ğ—¼ğ—¿ ğ‘– = ğŸ£, ğ‘˜ ğ—±ğ—¼:
      !   ğ“—áµ¢â‚– â† <ğ“ â‚–â‚Šâ‚â‹…ğ“ áµ¢>,
      !   ğ“ â‚–â‚Šâ‚ â† ğ“ â‚–â‚Šâ‚ - ğ“—áµ¢â‚–ğ“ áµ¢,
      ! ğ—²ğ—»ğ—± ğ—³ğ—¼ğ—¿
      ! ğ“—â‚–â‚Šâ‚,â‚– â† â€–ğ“ â‚–â‚Šâ‚â€–, ğ“ â‚–â‚Šâ‚ â† ğ“ â‚–â‚Šâ‚/ğ“—â‚–â‚Šâ‚,â‚–.  
      ! ----------------------
      sArr = QArr%Slice(k); tArr = QArr%Slice(k+1)
      call MatVec(mesh, tArr, sArr)
      do i = 1, k
        sArr = QArr%Slice(i); H(i,k) = Dot(mesh, tArr, sArr)
        call Sub(mesh, tArr, tArr, sArr, H(i,k))
      end do
      H(k+1,k) = Norm_2(mesh, tArr); call Scale(mesh, tArr, tArr, 1.0_dp/H(k+1,k))

      ! ----------------------
      ! Eliminate the last element in ğ“—
      ! and and update the rotation matrix:
      ! ğ—³ğ—¼ğ—¿ ğ‘– = ğŸ£, ğ‘˜ - ğŸ£ ğ—±ğ—¼:
      !   ğœ’ â† ğ’„ğ’”áµ¢â‹…ğ“—áµ¢â‚– + ğ’”ğ’áµ¢â‹…ğ“—áµ¢â‚Šâ‚,â‚–,
      !   ğ“—áµ¢â‚Šâ‚,â‚– â† -ğ’”ğ’áµ¢â‹…ğ“—áµ¢â‚– + ğ’„ğ’”áµ¢â‹…ğ“—áµ¢â‚Šâ‚,â‚– 
      !   ğ“—áµ¢â‚– â† ğœ’,
      ! ğ—²ğ—»ğ—± ğ—³ğ—¼ğ—¿
      ! ğ’„ğ’”â‚–, ğ’”ğ’â‚– â† ğ˜šğ˜ºğ˜®ğ˜–ğ˜³ğ˜µğ˜©ğ˜°(ğ“—â‚–â‚–, ğ“—â‚–â‚Šâ‚,â‚–),
      ! ğ“—â‚–â‚– â† ğ’„ğ’”â‚–â‹…ğ“—â‚–â‚– + ğ’”ğ’â‚–â‹…ğ“—â‚–â‚Šâ‚,â‚–,
      ! ğ“—â‚–â‚Šâ‚,â‚– â† ğŸ¢.
      ! ----------------------
      do i = 1, k - 1
        chi = cs(i)*H(i,k) + sn(i)*H(i+1,k)
        H(i+1,k) = -sn(i)*H(i,k) + cs(i)*H(i+1,k)
        H(i,k) = chi
      end do
      call SymOrtho(H(k,k), H(k+1,k), cs(k), sn(k), chi)
      H(k,k) = cs(k)*H(k,k) + sn(k)*H(k+1,k)
      H(k+1,k) = 0.0_dp

      ! ----------------------
      ! Update the residual vector:
      ! ğœ·â‚–â‚Šâ‚ â† -ğ’”ğ’â‚–â‹…ğœ·â‚–, ğœ·â‚– â† ğ’„ğ’”â‚–â‹…ğœ·â‚–,
      ! ğœ‘ â† |ğœ·â‚–â‚Šâ‚|,
      ! Check convergence for ğœ‘ and ğœ‘/ğœ‘Ìƒ.
      ! ----------------------
      beta(k+1) = -sn(k)*beta(k); beta(k) = cs(k)*beta(k)
      phi = abs(beta(k+1))
      if (params%Check(phi, phi/phiTilde)) exit

    end do

    ! ----------------------
    ! Check if restart is required.
    ! ----------------------
    converged = k <= gMaxIterGMRES
    if (.not.converged) k = gMaxIterGMRES

    ! ----------------------
    ! Compute ğ’™-solution:
    ! ğœ·â‚:â‚– â† (ğ“—â‚:â‚–,â‚:â‚–)â»Â¹ğœ·â‚:â‚–, 
    ! ğ—³ğ—¼ğ—¿ ğ‘– = 1, ğ‘˜ ğ—±ğ—¼:
    !   ğ’™ â† ğ’™ + ğœ·áµ¢ğ“ áµ¢.
    ! ğ—²ğ—»ğ—± ğ—³ğ—¼ğ—¿
    ! // Since ğ“—â‚:â‚– is upper triangular, 
    ! // operations can be combined:
    ! ğ—³ğ—¼ğ—¿ ğ‘– = ğ‘˜, ğŸ£, -ğŸ£ ğ—±ğ—¼:
    !   ğœ·áµ¢ â† (ğœ·áµ¢ - <ğ“—áµ¢,áµ¢â‚Šâ‚:â‚–â‹…ğœ·áµ¢â‚Šâ‚:â‚–>)/ğ“—áµ¢áµ¢,
    !   ğ’™ â† ğ’™ + ğœ·áµ¢ğ“ áµ¢.
    ! ğ—²ğ—»ğ—± ğ—³ğ—¼ğ—¿
    ! ----------------------
    do i = k, 1, -1
      beta(i) = beta(i) - dot_product(H(i,(i + 1):k), beta((i + 1):k))
      beta(i) = beta(i)/H(i,i)
      tArr = QArr%Slice(i); call Add(mesh, xArr, xArr, tArr, beta(i))
    end do
  end do

end subroutine Solve_GMRES

!! -=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=- !! 
!! Solve a linear operator equation: ğ“[ğ“Ÿ]ğ’™ = ğ’ƒ, ğ’™ = [ğ“Ÿ]ğ’š, using 
!! the Transpose-Free Quasi-Minimal Residual method (TFQMR).
!! 
!! References:
!! [1] Freund, Roland W. 
!!     â€œA Transpose-Free Quasi-Minimal Residual Algorithm 
!!      for Non-Hermitian Linear Systems.â€ 
!!     SIAM J. Sci. Comput. 14 (1993): 470-482.
!! [2] Freund, Roland W. 
!!     â€œTranspose-Free Quasi-Minimal Residual Methods 
!!      for Non-Hermitian Linear Systems.â€ (1994).
!! -=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=- !! 
subroutine Solve_TFQMR(mesh, xArr, bArr, MatVec, params, pre)
  class(tMesh), intent(in) :: mesh
  class(tArray), intent(in) :: bArr
  class(tArray), intent(inout) :: xArr
  class(tConvParams), intent(inout) :: params
  class(tPreconditioner), intent(inout), optional :: pre
  procedure(tMatVecFunc) :: MatVec

  error stop 'TFQMR solver is not implemented.'

end subroutine Solve_TFQMR

end module StormRuler_Solvers_MINRES
